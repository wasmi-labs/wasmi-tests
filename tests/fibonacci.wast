(module
    (func (export "fibonacci-iter") (param $n i64) (result i64)
        (local $n1 i64)
        (local $n2 i64)
        (local $tmp i64)
        (local $i i64)
        ;; return $n for N <= 1
        (if
            (i64.le_s (local.get $n) (i64.const 1))
            (then (return (local.get $n)))
        )
        (local.set $n1 (i64.const 1))
        (local.set $n2 (i64.const 1))
        (local.set $i (i64.const 2))
        ;;since we normally return n2, handle n=1 case specially
        (loop $continue
            (if
                (i64.lt_s (local.get $i) (local.get $n))
                (then
                    (local.set $tmp (i64.add (local.get $n1) (local.get $n2)))
                    (local.set $n1 (local.get $n2))
                    (local.set $n2 (local.get $tmp))
                    (local.set $i (i64.add (local.get $i) (i64.const 1)))
                    (br $continue)
                )
            )
        )
        (local.get $n2)
    )
)
(assert_return (invoke "fibonacci-iter" (i64.const  0)) (i64.const    0))
(assert_return (invoke "fibonacci-iter" (i64.const  1)) (i64.const    1))
(assert_return (invoke "fibonacci-iter" (i64.const  2)) (i64.const    1))
(assert_return (invoke "fibonacci-iter" (i64.const  3)) (i64.const    2))
(assert_return (invoke "fibonacci-iter" (i64.const  4)) (i64.const    3))
(assert_return (invoke "fibonacci-iter" (i64.const  5)) (i64.const    5))
(assert_return (invoke "fibonacci-iter" (i64.const  6)) (i64.const    8))
(assert_return (invoke "fibonacci-iter" (i64.const  7)) (i64.const   13))
(assert_return (invoke "fibonacci-iter" (i64.const  8)) (i64.const   21))
(assert_return (invoke "fibonacci-iter" (i64.const  9)) (i64.const   34))
(assert_return (invoke "fibonacci-iter" (i64.const 10)) (i64.const   55))
(assert_return (invoke "fibonacci-iter" (i64.const 11)) (i64.const   89))
(assert_return (invoke "fibonacci-iter" (i64.const 12)) (i64.const  144))
(assert_return (invoke "fibonacci-iter" (i64.const 13)) (i64.const  233))
(assert_return (invoke "fibonacci-iter" (i64.const 14)) (i64.const  377))
(assert_return (invoke "fibonacci-iter" (i64.const 15)) (i64.const  610))
(assert_return (invoke "fibonacci-iter" (i64.const 16)) (i64.const  987))
(assert_return (invoke "fibonacci-iter" (i64.const 17)) (i64.const 1597))
(assert_return (invoke "fibonacci-iter" (i64.const 18)) (i64.const 2584))
(assert_return (invoke "fibonacci-iter" (i64.const 19)) (i64.const 4181))

(module
    (func $fib_recursive (export "fibonacci-rec") (param $n i64) (result i64)
        (if
            (i64.le_s (local.get $n) (i64.const 1))
            (then (return (local.get $n)))
        )
        (return
            (i64.add
                (call $fib_recursive
                  (i64.sub (local.get $n) (i64.const 1))
                )
                (call $fib_recursive
                  (i64.sub (local.get $n) (i64.const 2))
                )
            )
        )
    )
)
(assert_return (invoke "fibonacci-rec" (i64.const  0)) (i64.const    0))
(assert_return (invoke "fibonacci-rec" (i64.const  1)) (i64.const    1))
(assert_return (invoke "fibonacci-rec" (i64.const  2)) (i64.const    1))
(assert_return (invoke "fibonacci-rec" (i64.const  3)) (i64.const    2))
(assert_return (invoke "fibonacci-rec" (i64.const  4)) (i64.const    3))
(assert_return (invoke "fibonacci-rec" (i64.const  5)) (i64.const    5))
(assert_return (invoke "fibonacci-rec" (i64.const  6)) (i64.const    8))
(assert_return (invoke "fibonacci-rec" (i64.const  7)) (i64.const   13))
(assert_return (invoke "fibonacci-rec" (i64.const  8)) (i64.const   21))
(assert_return (invoke "fibonacci-rec" (i64.const  9)) (i64.const   34))
(assert_return (invoke "fibonacci-rec" (i64.const 10)) (i64.const   55))
(assert_return (invoke "fibonacci-rec" (i64.const 11)) (i64.const   89))
(assert_return (invoke "fibonacci-rec" (i64.const 12)) (i64.const  144))
(assert_return (invoke "fibonacci-rec" (i64.const 13)) (i64.const  233))
(assert_return (invoke "fibonacci-rec" (i64.const 14)) (i64.const  377))
(assert_return (invoke "fibonacci-rec" (i64.const 15)) (i64.const  610))
(assert_return (invoke "fibonacci-rec" (i64.const 16)) (i64.const  987))
(assert_return (invoke "fibonacci-rec" (i64.const 17)) (i64.const 1597))
(assert_return (invoke "fibonacci-rec" (i64.const 18)) (i64.const 2584))
(assert_return (invoke "fibonacci-rec" (i64.const 19)) (i64.const 4181))

(module
    (func $fib_tail_recursive (param $n i64) (param $a i64) (param $b i64) (result i64)
        (if (i64.eqz (local.get $n))
            (then
                (return (local.get $a))
            )
        )
        (if (i64.eq (local.get $n) (i64.const 1))
            (then
                (return (local.get $b))
            )
        )
        (return_call $fib_tail_recursive
            (i64.sub (local.get $n) (i64.const 1))
            (local.get $b)
            (i64.add (local.get $a) (local.get $b))
        )
    )

    (func (export "fibonacci-tail") (param $n i64) (result i64)
        (return_call $fib_tail_recursive (local.get $n) (i64.const 0) (i64.const 1))
    )
)
(assert_return (invoke "fibonacci-tail" (i64.const  0)) (i64.const    0))
(assert_return (invoke "fibonacci-tail" (i64.const  1)) (i64.const    1))
(assert_return (invoke "fibonacci-tail" (i64.const  2)) (i64.const    1))
(assert_return (invoke "fibonacci-tail" (i64.const  3)) (i64.const    2))
(assert_return (invoke "fibonacci-tail" (i64.const  4)) (i64.const    3))
(assert_return (invoke "fibonacci-tail" (i64.const  5)) (i64.const    5))
(assert_return (invoke "fibonacci-tail" (i64.const  6)) (i64.const    8))
(assert_return (invoke "fibonacci-tail" (i64.const  7)) (i64.const   13))
(assert_return (invoke "fibonacci-tail" (i64.const  8)) (i64.const   21))
(assert_return (invoke "fibonacci-tail" (i64.const  9)) (i64.const   34))
(assert_return (invoke "fibonacci-tail" (i64.const 10)) (i64.const   55))
(assert_return (invoke "fibonacci-tail" (i64.const 11)) (i64.const   89))
(assert_return (invoke "fibonacci-tail" (i64.const 12)) (i64.const  144))
(assert_return (invoke "fibonacci-tail" (i64.const 13)) (i64.const  233))
(assert_return (invoke "fibonacci-tail" (i64.const 14)) (i64.const  377))
(assert_return (invoke "fibonacci-tail" (i64.const 15)) (i64.const  610))
(assert_return (invoke "fibonacci-tail" (i64.const 16)) (i64.const  987))
(assert_return (invoke "fibonacci-tail" (i64.const 17)) (i64.const 1597))
(assert_return (invoke "fibonacci-tail" (i64.const 18)) (i64.const 2584))
(assert_return (invoke "fibonacci-tail" (i64.const 19)) (i64.const 4181))
